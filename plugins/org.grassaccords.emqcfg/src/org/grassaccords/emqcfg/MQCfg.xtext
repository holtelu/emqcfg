grammar org.grassaccords.emqcfg.MQCfg with org.eclipse.xtext.common.Terminals

generate mQCfg 'http://www.grassaccords.org/emqcfg/MQCfg'

Model :
	(imports+=Import)*
	
	'name' name=STRING	
	
	('namingrules' namingrulesURI=STRING)?
	
	(elements+=TopLevelType)*
;

Import :
	'import' importURI=STRING;
	
TopLevelType:
	Node | QMgr | Channel | Cluster
;


Node : 'node' name=ID '{'
 'description' description=STRING
 ('qmgrs' (qmgrs+=[QMgr] (',' qmgrs+=[QMgr])*)?)?
'}'
;

QMgr : 'qmgr' name=ID '{'
 'description' description=STRING
 (interfaces+=Interface)+
'}'
;


terminal IPAddress :
	INT'.'INT'.'INT'.'INT
	;

Interface :
 'interface' name=ID '{'
  'host' host=STRING 
  'ip' ip=IPAddress
  'port' port=INT 
 '}'
;

QMgrBinding :
	qmgr=[QMgr]'@'interface=[Interface]
;	

ClusterQMgrBinding :
	(isRepository?='*')? qmgr=[QMgr]'@'interface=[Interface]
;	

Cluster :
	'cluster' name=ID '{'
		'description' description=STRING
		('members' members+=ClusterQMgrBinding (',' members+=ClusterQMgrBinding)*)?
	'}'	
	;

Channel :
 (bidirectional?='bidirectional')? 'channel' name=ID '{'
 'description' description=STRING 
 'src' srcQMgr=QMgrBinding
 'dst' dstQMgr=QMgrBinding
'}'
;
